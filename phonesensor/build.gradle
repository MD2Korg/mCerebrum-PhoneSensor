version = '1.1.2'


buildscript {
    repositories {
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {
        classpath 'io.fabric.tools:gradle:1.22.0'
    }
}
apply plugin: 'com.android.application'
apply plugin: 'io.fabric'
apply from: 'versioning.gradle'

repositories {
    maven { url 'https://maven.fabric.io/public' }
    maven { url 'https://jitpack.io' }
    mavenCentral()

}

def store_file = "keystore_missing"
if ( hasProperty('MD2K_RELEASE_STORE_FILE') ) {
    store_file = MD2K_RELEASE_STORE_FILE
} else if ( System.getenv('MD2K_RELEASE_STORE_FILE') ){
    store_file = System.getenv('MD2K_RELEASE_STORE_FILE')
} else {
    println "ERROR: " + store_file
}

def store_password = "keystore_password_missing"
if ( hasProperty('MD2K_RELEASE_STORE_PASSWORD') ) {
    store_password = MD2K_RELEASE_STORE_PASSWORD
} else if ( System.getenv('MD2K_RELEASE_STORE_PASSWORD') ){
    store_password = System.getenv('MD2K_RELEASE_STORE_PASSWORD')
} else {
    println "ERROR: " + store_password
}


def key_alias = "release_key_alias_missing"
if ( hasProperty('MD2K_RELEASE_KEY_ALIAS') ) {
    key_alias = MD2K_RELEASE_KEY_ALIAS
} else if ( System.getenv('MD2K_RELEASE_KEY_ALIAS') ){
    key_alias = System.getenv('MD2K_RELEASE_KEY_ALIAS')
} else {
    println "ERROR: " + key_alias
}

def key_password = "release_key_password_missing"
if ( hasProperty('MD2K_RELEASE_KEY_PASSWORD') ) {
    key_password = MD2K_RELEASE_KEY_PASSWORD
} else if ( System.getenv('MD2K_RELEASE_KEY_PASSWORD') ){
    key_password = System.getenv('MD2K_RELEASE_KEY_PASSWORD')
} else {
    println "ERROR: " + key_password
}


ext {
    travisBuild = System.getenv("TRAVIS") == "true"
    // allows for -Dpre-dex=false to be set
    preDexEnabled = "true".equals(System.getProperty("pre-dex", "true"))
}

android {
    signingConfigs {
        release {

            storeFile file(store_file)
            storePassword store_password
            keyAlias key_alias
            keyPassword key_password
        }
    }
    compileSdkVersion 25
    buildToolsVersion '25.0.0'
    defaultConfig {
        applicationId "org.md2k.phonesensor"
        minSdkVersion 19
        targetSdkVersion 25
        versionCode buildVersionCode()
        versionName version
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        multiDexEnabled true
    }
    dexOptions {
        // Skip pre-dexing when running on Travis CI or when disabled via -Dpre-dex=false.
        preDexLibraries = preDexEnabled && !travisBuild
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
            applicationVariants.all { variant ->
                variant.outputs.each { output ->
                    output.outputFile = new File(output.outputFile.parent, output.outputFile.name.replace("-release", "${variant.versionName}"))
                }
            }
        }
    }
    lintOptions {
        abortOnError false
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile project(':datakitapi')
    compile project(':utilities')
    compile 'com.google.android.gms:play-services:10.0.1'
    compile('com.crashlytics.sdk.android:crashlytics:2.6.1@aar') {
        transitive = true;
    }

    // Required -- JUnit 4 framework
    testCompile 'junit:junit:4.12'
    // Unit testing dependencies
    androidTestCompile 'junit:junit:4.12'
    // Set this dependency if you want to use the Hamcrest matcher library
    androidTestCompile 'org.hamcrest:hamcrest-library:1.3'
    // more stuff, e.g., Mockito
    androidTestCompile 'com.android.support.test:runner:0.5'
    androidTestCompile 'com.android.support.test:rules:0.5'
    androidTestCompile 'com.android.support:support-annotations:25.0.1'
    compile 'com.android.support:multidex:1.0.1'

}
